

%macro condi(prg,prgL,yr);
Data DME;
set &prg..&prgl._enc_cy20&yr.(drop=sxdate: POA:);
where svccod in (&A_DME.) and membno not in ('' '000000000');
DME=1;
run;

proc sort data=DME nodupkey out=&prg._DME(keep=membno DME);
by membno;
run;


proc sort data=&prg..&prgl._enc_cy20&yr.(drop=sxdate: POA:) nodupkey 
	out=&prg._diag_20&yr.;
by membno clmno plancod;
run;


Data &prg._diag_20&yr.;
set &prg._diag_20&yr.;
array D(*) diagn1-diagn25;
do i=1 to dim(D) while ( D(i)^='');
	if d(i) in: (&A_Cancer) or d(i) in:(&A_heart) or d(i) in:(&A_renal) or d(i) in:(&A_stroke)
	or d(i) in:(&A_alzheimer) or d(i) in:(&A_cirrhosis) or d(i) in:(&A_frailty)
	or d(i) in:(&A_lung_failure) or d(i) in:(&A_neurodegenerative) or d(i)in (&A_hiv_aids)
	then adult_con=1;
	if d(i) in:(&A_diabetes_w_complications) then diabete=1;
	if d(i) in:(&A_diabetes_severe_complications) then comorbid=1;
	/*pediatric */
	if d(i) in: (&P_neuro) or d(i) in:(&P_cardio) or d(i) in:(&P_resp) 
	or d(i) in: (&P_renal) or d(i) in:(&P_gastro) or d(i) in:(&P_hemo)
	or d(i) in: (&P_metab) or d(i) in:(&P_congen) or d(i) in:(&P_maligcy)
	or d(i) in: (&P_premature) or  d(i) in:(&P_misc) 
	then ped_con=1;
end;

run;
proc summary data=&prg._diag_20&yr.;
var adult_con diabete comorbid ped_con;
class membno;
output out=member_level_&yr. max=;
run;

proc sort data=member_level_&yr.;
where membno ^='' and (adult_con=1 or ped_con=1 or diabete=1 or comorbid=1  );
by membno;
run;

data temp.&prg._member_condition_&yr.;
merge member_level_&yr.(in=in1) &prg._DME;
by membno;
if (diabete=1 and comorbid=1) or DME=1 then adult_con=1;

if adult_con=1 or ped_con=1;
run;

%mend condi;

%condi(SP, STARPLUS, 21)
%condi(ST, STAR, 21)
%condi(SK, STARkids, 21)
%condi(p200, PRG200, 21)

%condi(SP, STARPLUS, 22)
%condi(ST, STAR, 22)
%condi(SK, STARkids, 22)
%condi(p200, PRG200, 22)
